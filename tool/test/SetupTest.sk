;;; Tests for setup.
;;;
;;; This file contains similar tests written for the "sk-setup"
;;; package. See the main test file "setup/test/Main.hs" for sk-setup.

(require SK.Core)

(defmodule SetupTest
  (export
   setupTests)
  (use
   ;; base
   (Control.Exception (catch throw))
   (System.Environment (unsetEnv withArgs))
   (System.IO.Error (isDoesNotExistError))

   ;; directory
   (System.Directory
    (getCurrentDirectory removeFile setCurrentDirectory))

   ;; filepath
   (System.FilePath (</>))

   ;; hspec
   (Test.Hspec)

   ;; process
   (System.Process (readProcess))

   ;; Internal
   (SK.Setup (skMakeMain))))

(defn (setupTests Spec)
  (do (<- cwd (runIO getCurrentDirectory))
      (<- pkgdbs (runIO getStackPackageDbs))
      (runIO (unsetEnv "GHC_PACKAGE_PATH"))
      (after_ (setCurrentDirectory cwd)
              (before_ (removeTixWhenFound cwd "p02")
                       (buildPackage cwd pkgdbs "p02")))))

(defn (buildPackage (-> FilePath [FilePath] String Spec))
  [cwd pkgdbs name]
  (where (describe (<> "package " name)
                   (it "should compile and pass the tests" work))
    (defn work
      (mapM_ run [(setCurrentDirectory (pkgdir cwd name))
                  (setup configure-args)
                  (setup ["build"])
                  (setup ["test"])
                  (setup ["haddock"])
                  (setup ["clean"])]))
    (defn run [act]
      (shouldReturn act ()))
    (defn pkgdb-flags
      (++ ["--package-db=clear"
           "--package-db=global"]
          (map (\ p (<> "--package-db=" p)) pkgdbs)))
    (defn configure-args
      (: "configure" (++ pkgdb-flags ["--enable-tests"])))))

(defn (pkgdir (-> FilePath String FilePath))
  [cwd name]
  (</> cwd (</> "test" (</> "data" name))))

(defn (setup (-> [String] (IO ())))
  (flip withArgs skMakeMain))

(defn (removeTixWhenFound (-> FilePath String (IO ())))
  [cwd name]
  (catch (removeFile (</> (pkgdir cwd name) "sk.tix"))
    (\ e (if (isDoesNotExistError e)
             (return ())
             (throw e)))))

(defn (getStackPackageDbs (IO [String]))
  (do (<- snapshot-pkgdb (getStackOutput ["path" "--snapshot-pkg-db"]))
      (<- local-pkgdb (getStackOutput ["path" "--local-pkg-db"]))
      (return (map (takeWhile (/= \\n))
                   [snapshot-pkgdb local-pkgdb]))))

(defn (getStackOutput (-> [String] (IO String)))
  [args]
  (readProcess "stack" args ""))
