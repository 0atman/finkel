;;; Module for showing versions.

(:require SK.Core)

(defmodule SK.Tool.Version
  (export
   versionMain)
  (use
   ;; base
   (Control.Monad.IO.Class ((MonadIO ..)))
   (Data.Version (showVersion))
   (System.Console.GetOpt
    ((ArgDescr ..) (ArgOrder ..) (OptDescr ..) getOpt usageInfo))
   (System.Environment (getProgName))

   ;; ghc
   (Config (cProjectVersion))

   ;; sk-kernel
   (qualified Paths_sk_kernel)

   ;; sk-lang
   (qualified Paths_sk_lang)

   ;; Internal
   (SK.Tool.CLI)))



;;; Exported

(defn (:: versionMain (=> (CLI m) (-> [String] (m ()))))
  [args]
  (case (getOpt Permute version-descrs args)
    (, os _ []) (show-version (foldr const AllVersions os))
    (, _ _ es)  (do (putString (concat es))
                    print-version-help)))


;;; Internal

(data VersionMode
  AllVersions
  GhcVersion
  KernelVersion
  LangVersion
  VersionHelp)

(defn (:: version-descrs [OptDescr VersionMode])
  [(Option [#'a] ["all"]
           (NoArg AllVersions)
           "show all versions (default)")
   (Option [#'g] ["ghc"]
           (NoArg GhcVersion)
           "show ghc version")
   (Option [#'k] ["kernel"]
           (NoArg KernelVersion)
           "show sk kernel version")
   (Option [#'l] ["lang"]
           (NoArg LangVersion)
           "show sk lang version")
   (Option [#'h] ["help"]
           (NoArg VersionHelp)
           "show this help and exit")])

(defn (:: show-version (=> (CLI m) (-> VersionMode (m ()))))
  [mode]
  (case mode
    AllVersions (do print-lang-version
                    print-kernel-version
                    print-ghc-version)
    GhcVersion print-ghc-version
    KernelVersion print-kernel-version
    LangVersion print-lang-version
    VersionHelp print-version-help))

(defn (:: print-ghc-version (=> (CLI m) (m ())))
  (putString (++ "ghc " cProjectVersion)))

(defn (:: print-kernel-version (=> (CLI m) (m ())))
  (putString (++ "sk-kernel " sk-kernel-version)))

(defn (:: print-lang-version (=> (CLI m) (m ())))
  (putString (++ "sk-lang " sk-lang-version)))

(defn (:: print-version-help (=> (CLI m) (m ())))
  (do (<- name (liftIO getProgName))
      (putString
       (unlines
        [(concat ["USAGE: " name " version [OPTIONS]"])
         ""
         "Show version information."
         ""
         (usageInfo "OPTIONS:\n" version-descrs)]))))

(defn (:: sk-lang-version String)
  (showVersion Paths_sk_lang.version))

(defn (:: sk-kernel-version String)
  (showVersion Paths_sk_kernel.version))
