;;; Module for showing versions.

(require SK.Core)

(defmodule SK.Tool.Version
  (export
   versionMain
   (VersionUI ..))
  (use
   ;; base
   (Control.Monad.IO.Class ((MonadIO ..)))
   (Data.Version (showVersion))
   (System.Console.GetOpt
    ((ArgDescr ..) (ArgOrder ..) (OptDescr ..) getOpt usageInfo))
   (System.Environment (getProgName))

   ;; ghc
   (Config (cProjectVersion))

   ;; sk-kernel
   (qualified Paths_sk_kernel)

   ;; sk-lang
   (qualified Paths_sk_lang)

   ;; Internal
   (SK.Tool.Help)))



;;; Exported

(class (=> (HelpUI m) (VersionUI m))
  (:: printVersion (-> String (m ())))
  (= printVersion printHelp))

(instance (VersionUI IO))

(defn (versionMain (=> (VersionUI m) (-> [String] (m ()))))
  [args]
  (case (getOpt Permute version-descrs args)
    (, os _ []) (show-version (foldr const AllVersions os))
    (, _ _ es)  (do (printVersion (concat es))
                    print-version-help)))


;;; Internal

(data VersionMode
  AllVersions
  GhcVersion
  KernelVersion
  LangVersion
  VersionHelp)

(defn (version-descrs [OptDescr VersionMode])
  [(Option [\a] ["all"]
           (NoArg AllVersions)
           "show all versions (default)")
   (Option [\g] ["ghc"]
           (NoArg GhcVersion)
           "show ghc version")
   (Option [\k] ["kernel"]
           (NoArg KernelVersion)
           "show sk kernel version")
   (Option [\l] ["lang"]
           (NoArg LangVersion)
           "show sk lang version")
   (Option [\h] ["help"]
           (NoArg VersionHelp)
           "show this help and exit")])

(defn (show-version (=> (VersionUI m) (-> VersionMode (m ()))))
  [mode]
  (case mode
    AllVersions (do print-lang-version
                    print-kernel-version
                    print-ghc-version)
    GhcVersion print-ghc-version
    KernelVersion print-kernel-version
    LangVersion print-lang-version
    VersionHelp print-version-help))

(defn (print-ghc-version (=> (VersionUI m) (m ())))
  (printVersion (++ "ghc " cProjectVersion)))

(defn (print-kernel-version (=> (VersionUI m) (m ())))
  (printVersion (++ "sk-kernel " sk-kernel-version)))

(defn (print-lang-version (=> (VersionUI m) (m ())))
  (printVersion (++ "sk-lang " sk-lang-version)))

(defn (print-version-help (=> (HelpUI m) (m ())))
  (do (<- name (liftIO getProgName))
      (printHelp
       (unlines
        [(concat ["USAGE: " name " version [OPTIONS]"])
         ""
         "Show version information."
         ""
         (usageInfo "OPTIONS:\n" version-descrs)]))))

(defn (sk-lang-version String)
  (showVersion Paths_sk_lang.version))

(defn (sk-kernel-version String)
  (showVersion Paths_sk_kernel.version))
