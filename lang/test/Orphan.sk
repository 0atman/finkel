;;;; Orphan instances for QuickCheck

;;; This module contains duplicated codes with `Orphan' module used by
;;; sk-kernel test. At the moment, could not find a nice way to avoid
;;; adding QuickCheck package dependency without code duplication.

#p(OPTIONS_GHC -fno-warn-orphans)

(require SK.Core)

(defmodule Orphan
  (use
   ;; ghc
   (BasicTypes ((FractionalLit ..)))
   (FastString (unpackFS))

   ;; QuickCheck
   (Test.QuickCheck ((Arbitrary ..) (Gen)
                     arbitraryUnicodeChar elements getUnicodeString
                     listOf oneof scale variant))

   ;; sk-kernel
   (Language.SK.Form)))

(instance (Arbitrary Atom)
  (= arbitrary
    (where (oneof [(return AUnit)
                   (fmap aSymbol symbolG)
                   (fmap AChar arbitraryUnicodeChar)
                   (fmap AString stringG)
                   (fmap AInteger arbitrary)
                   (fmap aFractional (:: arbitrary (Gen Double)))])
      (= headChars
        (concat [[\A .. \Z] [\a .. \z] haskellOpChars']))
      (= haskellOpChars'
        (: \_ (filter (flip notElem "#-|") haskellOpChars)))
      (= tailChars
        (++ headChars "0123456789'-"))
      (= symbolG
        (do (<- x (elements headChars))
            (<- xs (listOf (elements tailChars)))
            (return (: x xs))))
      (= stringG
        (fmap getUnicodeString arbitrary)))))

(instance (=> (Arbitrary a) (Arbitrary (Form a)))
  (= arbitrary
    (oneof [(fmap Atom arbitrary)
            (fmap List (listOf (scale (flip div 3) arbitrary)))
            (fmap HsList (listOf (scale (flip div 3) arbitrary)))]))
  (= shrink x
    (case x
      (Atom _)    []
      (List xs)   (++ (map unCode xs) (map List (shrink xs)))
      (HsList xs) (++ (map unCode xs) (map HsList (shrink xs)))
      TEnd        [])))

(instance (=> (Arbitrary a) (Arbitrary (LForm a)))
  (= arbitrary (fmap (. LForm (L skSrcSpan)) arbitrary)))
